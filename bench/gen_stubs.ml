(** Generate EverParse schemas and benchmark infrastructure.

    Usage: gen_stubs.exe <schema_dir>

    1. Generates .3d files with output pattern (extern callbacks). 2. Runs
    EverParse to produce C validators. 3. Writes the default [<Name>_Fields]
    plug (typed struct + switch dispatchers) and the FFI parse / timed-loop C
    stubs that consume it. *)

let ml_only = Array.length Sys.argv > 1 && Sys.argv.(1) = "--ml-only"
let schema_dir = if Array.length Sys.argv > 1 then Sys.argv.(1) else "schemas"

(* Keep full schemas for the optional scenario-benchmark tier. *)
let structs =
  Demo_bench_cases.projection_structs
  @ [ Space.clcw_struct; Space.packet_struct; Space.tm_frame_struct ]

let struct_size s =
  match Wire.Everparse.Raw.struct_size s with
  | Some n -> n
  | None ->
      Fmt.failwith "benchmark schema %s has variable-length fields"
        (Wire.Everparse.Raw.struct_name s)

let projection_structs = Demo_bench_cases.projection_structs

let generate_stub_registry ppf structs projection_structs =
  let pr fmt = Fmt.pf ppf fmt in
  pr "type stubs = {\n";
  pr "  check : bytes -> bool;\n";
  pr "  ffi_parse : bytes -> int -> unit;\n";
  pr "  loop : bytes -> int -> int -> int;\n";
  pr "  projected_int : bytes -> int -> int;\n";
  pr "  projected_int64 : bytes -> int -> int64;\n";
  pr "}\n\n";
  pr "let stubs_of_name = function\n";
  List.iter
    (fun s ->
      let name = Wire.Everparse.Raw.struct_name s in
      let lower = String.lowercase_ascii name in
      let proj_int, proj_int64 =
        let is_proj =
          List.exists
            (fun p -> Wire.Everparse.Raw.struct_name p = name)
            projection_structs
        in
        if is_proj then
          match Wire.Everparse.Raw.field_kinds s with
          | [ (_, Wire.Everparse.Raw.K_int64) ] ->
              ("(fun _ _ -> 0)", Fmt.str "%s_projected_int64" lower)
          | [ _ ] -> (Fmt.str "%s_projected_int" lower, "(fun _ _ -> 0L)")
          | _ -> ("(fun _ _ -> 0)", "(fun _ _ -> 0L)")
        else ("(fun _ _ -> 0)", "(fun _ _ -> 0L)")
      in
      (* [ffi_parse] uses [_parse_k] directly (the single C entry point)
         with a no-op continuation. The OCaml-side record-returning
         [_parse] wrapper adds an extra C-to-OCaml callback hop just to
         build a record we'd then discard; the bench measures pure
         validation cost, so skip the wrapper. *)
      let n_args =
        List.length
          (Wire.Everparse.Raw.field_kinds
             (List.find
                (fun p -> Wire.Everparse.Raw.struct_name p = name)
                (structs @ projection_structs)))
      in
      let cont =
        "(fun "
        ^ String.concat " " (List.init n_args (fun _ -> "_"))
        ^ " -> ())"
      in
      pr
        "  | %S -> { check = %s_check; ffi_parse = (fun b off -> %s_parse_k %s \
         b off); loop = %s_loop; projected_int = %s; projected_int64 = %s }\n"
        name lower lower cont lower proj_int proj_int64)
    structs;
  pr "  | name -> failwith (\"C_stubs: unknown schema \" ^ name)\n"

let generate_ml oc =
  output_string oc (Wire_stubs.to_ml_stubs structs);
  let ppf = Format.formatter_of_out_channel oc in
  let pr fmt = Fmt.pf ppf fmt in
  List.iter
    (fun s ->
      let lower = String.lowercase_ascii (Wire.Everparse.Raw.struct_name s) in
      pr "external %s_check : bytes -> bool = \"caml_wire_%s_check\"\n\n" lower
        lower)
    structs;
  pr "(* Timed C benchmark loops *)\n\n";
  List.iter
    (fun s ->
      let lower = String.lowercase_ascii (Wire.Everparse.Raw.struct_name s) in
      pr "external %s_loop : bytes -> int -> int -> int = \"ep_loop_%s\"\n\n"
        lower lower)
    structs;
  pr "(* Projected field extraction *)\n\n";
  List.iter
    (fun s ->
      let lower = String.lowercase_ascii (Wire.Everparse.Raw.struct_name s) in
      match Wire.Everparse.Raw.field_kinds s with
      | [ (_, kind) ] ->
          let suffix =
            match kind with
            | Wire.Everparse.Raw.K_int64 -> "_int64"
            | _ -> "_int"
          in
          pr "let %s_projected%s = %s_parse_k Fun.id\n\n" lower suffix lower
      | _ -> ())
    projection_structs;
  pr "\n(* Per-schema stub registry *)\n\n";
  generate_stub_registry ppf structs projection_structs;
  Format.pp_print_flush ppf ()

let generate_c oc =
  output_string oc (Wire_stubs.to_c_stubs structs);
  let ppf = Format.formatter_of_out_channel oc in
  let pr fmt = Fmt.pf ppf fmt in
  pr "\n/* -- Timed C benchmark loops -- */\n\n";
  pr "#include \"bench_common.h\"\n\n";
  List.iter
    (fun s ->
      let name = Wire.Everparse.Raw.struct_name s in
      let ep = Wire_3d.everparse_name name in
      let lower = String.lowercase_ascii name in
      let n_fields = List.length (Wire.Everparse.Raw.field_names s) in
      ignore n_fields;
      pr "CAMLprim value caml_wire_%s_check(value v_buf) {\n" lower;
      pr "  CAMLparam1(v_buf);\n";
      pr "  uint8_t *data = (uint8_t *)Bytes_val(v_buf);\n";
      pr "  uint32_t len = caml_string_length(v_buf);\n";
      pr "  %sFields ctx = {0};\n" ep;
      pr
        "  uint64_t r = %sValidate%s((WIRECTX *) &ctx, NULL, bench_err, data, \
         len, 0);\n"
        ep ep;
      pr "  CAMLreturn(Val_bool(EverParseIsSuccess(r)));\n";
      pr "}\n\n";
      let item_size = struct_size s in
      pr "CAMLprim value ep_loop_%s(value v_buf, value v_off, value v_n) {\n"
        lower;
      pr "  CAMLparam3(v_buf, v_off, v_n);\n";
      pr "  uint8_t *buf = (uint8_t *)Bytes_val(v_buf) + Int_val(v_off);\n";
      pr "  uint32_t len = caml_string_length(v_buf) - Int_val(v_off);\n";
      pr "  const uint32_t item_size = %d;\n" item_size;
      pr "  const uint32_t n_items = len / item_size;\n";
      pr "  int count = Int_val(v_n);\n";
      pr "  volatile uint64_t result = 0;\n";
      pr "  if (n_items == 0) CAMLreturn(Val_int(0));\n";
      pr "  %sFields ctx = {0};\n" ep;
      pr "  int64_t t0 = now_ns();\n";
      pr "  for (int i = 0; i < count; i++) {\n";
      pr "    uint8_t *item = buf + ((uint32_t)i %% n_items) * item_size;\n";
      pr
        "    result = %sValidate%s((WIRECTX *) &ctx, NULL, bench_err, item, \
         item_size, 0);\n"
        ep ep;
      pr "  }\n";
      pr "  (void)result;\n";
      pr "  int64_t t1 = now_ns();\n";
      pr "  CAMLreturn(Val_int(t1 - t0));\n";
      pr "}\n\n")
    structs;
  Format.pp_print_flush ppf ()

let () =
  if ml_only then generate_ml stdout
  else begin
    let schemas = List.map Wire.Everparse.schema_of_struct structs in

    (* 1. Generate .3d *)
    Wire_3d.generate_3d ~outdir:schema_dir schemas;

    (* 2. Run EverParse, then write default ExternalTypedefs + _Fields plug *)
    let quiet = Sys.getenv_opt "EVERPARSE_VERBOSE" = None in
    Wire_3d.run_everparse ~quiet ~outdir:schema_dir schemas;
    Wire_3d.write_external_typedefs ~outdir:schema_dir schemas;
    Wire_3d.write_fields ~outdir:schema_dir schemas;

    (* 3. Generate c_stubs.c *)
    let oc = open_out "c_stubs.c" in
    generate_c oc;
    close_out oc;

    Fmt.pr "Generated %d schemas in %s/@." (List.length structs) schema_dir;
    Fmt.pr "Generated c_stubs.c@."
  end
